# For an example of how to make your own CMake project that uses OpenSim,
# see SampleCMakeLists.txt.
#
# This config file defines the following variables:
#   @CMAKE_PROJECT_NAME@_FOUND -- True if search for libraries and headers
#                                 succeeds.
#   @CMAKE_PROJECT_NAME@_ROOT_DIR -- The installation directory.
#   @CMAKE_PROJECT_NAME@_LIB_DIR -- The directory containing libraries.
#                                   Suitable for link_directories().
#   @CMAKE_PROJECT_NAME@_INCLUDE_DIRS -- Suitable for include_directories().
#   @CMAKE_PROJECT_NAME@_LIBRARIES -- Suitable for target_link_libraries().
#   @CMAKE_PROJECT_NAME@_JAVA_WRAPPING -- Boolean indicating whether or not
#                                         OpenSim was built to include Java
#                                         wrapping.
#   @CMAKE_PROJECT_NAME@_PYTHON_WRAPPING -- Boolean indicating whether or not
#                                           OpenSim was built to include Python
#                                           wrapping.
#   @CMAKE_PROJECT_NAME@_JAR_FILE -- Path to jar file.
#   @CMAKE_PROJECT_NAME@_JAVA_FILES_DIR -- Directory containing the Java source
#                                          files.
#                                            
# This file will also find OpenSim dependencies (Simbody and BTK); you do not
# need to use `find_package(Simbody)` in your own project. If OpenSim's
# installation does *not* contain Simbody and BTK, you might need to set
# CMAKE_PREFIX_PATH to the directory or directories containing these
# dependencies.
#
# Adapted from SimbodyConfig.cmake
#

# To make the OpenSim installation relocatable:
@PACKAGE_INIT@

set_and_check(@CMAKE_PROJECT_NAME@_ROOT_DIR "@PACKAGE_CMAKE_INSTALL_PREFIX@")

# LIB_DIR
# -------
set_and_check(@CMAKE_PROJECT_NAME@_LIB_DIR "@PACKAGE_CMAKE_INSTALL_LIBDIR@")

# TODO
#if(@WITH_BTK@)
#    list(APPEND @CMAKE_PROJECT_NAME@_LIB_DIR "@BTK_LIBRARY_DIRS@")
#endif()

# INCLUDE_DIRS
# ------------
list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
    "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@"
    # TODO change this.
    "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@/Vendors/lepton/include"
    )


# LIBRARIES
# ---------
include("@PACKAGE_OPENSIM_INSTALL_CMAKEDIR@/@CMAKE_PROJECT_NAME@Targets.cmake")

# The osimTools target uses all the other targets.
# This makes a distinction between release and debug libraries.
list(APPEND @CMAKE_PROJECT_NAME@_LIBRARIES osimTools)

check_required_components(OpenSim)

# Python/Java Wrapping
#---------------------
set(@CMAKE_PROJECT_NAME@_PYTHON_WRAPPING @BUILD_PYTHON_WRAPPING@)

set(@CMAKE_PROJECT_NAME@_JAVA_WRAPPING @BUILD_JAVA_WRAPPING@)
set(@CMAKE_PROJECT_NAME@_JAVA_FILES_DIR 
    @PACKAGE_CMAKE_INSTALL_PREFIX@/@OPENSIM_INSTALL_JAVASRCDIR@/org)
set(@CMAKE_PROJECT_NAME@_JAR_FILE 
    @PACKAGE_CMAKE_INSTALL_PREFIX@/@OPENSIM_INSTALL_JAVAJARDIR@/@SWIG_JAVA_JAR_NAME@)

# Dependencies
# ------------
if (@OPENSIM_COPY_DEPENDENCIES@) # OPENSIM_COPY_DEPENDENCIES
    # Find the copy of Simbody within the OpenSim installation.
    find_package(Simbody @SIMBODY_VERSION_TO_USE@
        PATHS "@PACKAGE_OPENSIM_INSTALL_SIMBODYDIR@" NO_MODULE NO_DEFAULT_PATH)

    # if(@WITH_BTK@) # WITH_BTK
    #     link_directories("@PACKAGE_CMAKE_INSTALL_LIBDIR@")
    #     # Alternative (if we installed the BTKConfig.cmake and related files.
    #     #find_package(BTK REQUIRED PATHS "@BTK_DIR@" NO_MODULE NO_DEFAULT_PATH)
    #     # We could omit this line if BTK exported its targets:
    #     #include("${BTK_USE_FILE}")
    # endif()
else()
    # Find the correct version anywhere on the machine.
    include(CMakeFindDependencyMacro)
    find_dependency(Simbody @SIMBODY_VERSION_TO_USE@)
    # if(@WITH_BTK@) # WITH_BTK
    #     find_dependency(BTK)
    #     # We could omit this line if BTK exported its targets:
    #     include("${BTK_USE_FILE}")
    # endif()
endif()


